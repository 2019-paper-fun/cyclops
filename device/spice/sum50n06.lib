*April 10, 2006
*Doc. ID: 78156, S-60544, Rev. B
*File Name: SUM50N06-16L_PS.txt and SUM50N06-16L_PS.lib
*This document is intended as a SPICE modeling guideline and does not
*constitute a commercial product data sheet.  Designers should refer to the
*appropriate data sheet of the same number for guaranteed specification
*limits.
.SUBCKT SUM50N06-16L 4 1 2
M1  3 1 2 2 NMOS W=2615711u L=0.25u 
M2  2 1 2 4 PMOS W=2615711u L=0.45u
R1  4 3     RTEMP 82E-4
CGS 1 2     950E-12
DBD 2 4     DBD
******************************************************************* 
.MODEL  NMOS       NMOS ( LEVEL  = 3               TOX    = 7E-8
+ RS     = 37E-4          RD     = 0               NSUB   = 1.78E17 
+ KP     = 9.1E-6         UO     = 650             
+ VMAX   = 0              XJ     = 5E-7            KAPPA  = 1E-4
+ ETA    = 1E-4           TPG    = 1  
+ IS     = 0              LD     = 0               
+ CGSO   = 0              CGDO   = 0               CGBO   = 0 
+ NFS    = 0.8E12         DELTA  = 0.1)
******************************************************************* 
.MODEL  PMOS       PMOS ( LEVEL  = 3               TOX    = 7E-8
+NSUB    = 2.8E16         TPG    = -1)   
******************************************************************* 
.MODEL DBD D (CJO=700E-12 VJ=0.38 M=0.35
+RS=0.1 FC=0.5 IS=1E-12 TT=5E-8 N=1 BV=60.2)
******************************************************************* 
.MODEL RTEMP RES (TC1=7E-3 TC2=5.5E-6)
******************************************************************* 
.ENDS

